model Buffers

class Element
  attributes
    content:Integer
end

class Buffer 
  attributes 
    capacity:Integer
  operations
    put(p:Element)
      begin
          insert( self, p ) into Containment;
      end
      pre notFull: (self.elements->size() < capacity)
      post ElementAdded: (self.elements = self.elements@pre->append(p))

    get():Element
      begin
         result:=self.elements->at(1);
         delete( self, result) from Containment;
      end
      pre notEmpty: self.elements->size()>0
      post FirstElementRemoved: 
         result = self.elements@pre->at(1) and 
         self.elements@pre=self.elements->prepend(result) 

    size():Integer = self.elements->size()
    
  statemachines
    psm Buffer
      states
        s: initial 
        Empty [self.elements->size()=0]
        Normal
        Full [self.elements->size()=self.capacity]    
      transitions
        s -> Empty
        Empty -> Normal { [self.capacity>1] put() }
        Normal -> Normal { [self.elements->size()<capacity-1] put() }
        Normal -> Full { [(self.capacity>1) and (self.elements->size()=capacity-1)] put() }
        Empty -> Full { [self.capacity=1] put() }
--      Empty -> Empty { size() }   -- query operations do not change system state
--      Normal -> Normal { size() } -- query operations do not change system state
--      Full -> Full { size() }   -- query operations do not change system state
        Full -> Empty { [self.capacity=1] get() }
        Full -> Normal { [self.capacity>1] get() }
        Normal -> Normal { [(self.capacity>1) and (self.elements->size()>1)] get() }
        Normal -> Empty { [self.elements->size()=1] get() }
    end
end

aggregation Containment between
  Buffer [1] role buffer 
  Element [*] role elements ordered
end 

constraints

context Buffer inv AtLeastOneCell:
  self.capacity>0
